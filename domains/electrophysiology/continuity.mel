/*
 * <license>
 * Copyright (c) 2016-2017, Lawrence Livermore National Security,
 * LLC. Produced at the Lawrence Livermore National Laboratory. Written
 * by Robert Blake <blake14@llnl.gov>.
 *
 * LLNL-CODE-720003.
 * All rights reserved.
 *
 * This file is part of MELODEE. For details, see
 * http://github.com/llnl/melodee.
 *
 * Licensed under the Apache License, Version 2.0 (the "Licensee"); you
 * may not use this file except in compliance with the License.  You may
 * obtain a copy of the License at:
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the license.
 * </license> 
 */

integrate t {ms};

shared V {mV};
shared Iion {uA/uF};
shared V_init {mV};

shared iStim {uA/uF};

subsystem Vm {
  provides diffvar @coupled V;
  V.init = V_init;
  V.diff = -Iion + iStim;
}

subsystem noStim {
  provides iStim;
  iStim = 0;
}

shared bcl_time {ms};

subsystem bcl_time {
  provides bcl_time;

  @param offset {ms} = 0;
  @param n {1} = 1;
  @param bcl {ms} = 1000;

  if (t < offset) {
     bcl_time = bcl*1000+t-offset;
  } else { 
    beat = floor((t-offset)/bcl);
    if (beat >= n) {
      beat = n-1;
    }
    bcl_time = t-offset - beat*bcl;
  }
}

shared stim_time {ms};
shared stim_amp {uA/uF};

subsystem squareStim {
  @param duration {ms} = 1;

  provides iStim = 0;
  if (stim_time < duration) {
     iStim = stim_amp;
  }
}

subsystem bclDefault {
  use Vm;
  use bcl_time;
  use squareStim {
    export stim_time as bcl_time;
  }
}
